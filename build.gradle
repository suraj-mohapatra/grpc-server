plugins {
    id 'java'
    id 'com.google.protobuf' version '0.9.4'
    id 'com.github.johnrengelman.shadow' version '8.1.1'
}

group = 'com.uglyeagle'
version = '1.0.0'

sourceCompatibility = 17
targetCompatibility = 17

repositories {
    mavenCentral()
}

ext {
    grpcVersion = '1.79.0'
    protobufVersion = '4.33.5'
}

dependencies {
    implementation platform("io.grpc:grpc-bom:${grpcVersion}")
    implementation "io.grpc:grpc-netty-shaded"
    implementation "io.grpc:grpc-protobuf"
    implementation "io.grpc:grpc-stub"
    implementation "com.google.protobuf:protobuf-java:${protobufVersion}"
    implementation "javax.annotation:javax.annotation-api:1.3.2"
}

protobuf {
    protoc {
        artifact = "com.google.protobuf:protoc:${protobufVersion}"
    }

    plugins {
        grpc {
            artifact = "io.grpc:protoc-gen-grpc-java:${grpcVersion}"
        }
    }

    generateProtoTasks {
        all().each { task ->
            task.plugins {
                grpc {}
            }
        }
    }
}

//jar {
//    manifest {
//        attributes 'Main-Class': 'com.uglyeagle.GrpcServer'
//    }
//}

shadowJar {
    archiveBaseName.set('grpc-server')
    archiveVersion.set('1.0.0')
    archiveClassifier.set('')

    manifest {
        attributes 'Main-Class': 'com.uglyeagle.GrpcServer'
    }
}


build {
    dependsOn shadowJar
}